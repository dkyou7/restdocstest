plugins {
    id 'org.springframework.boot' version '2.5.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'

    // Asciidoctor
    // AsciiDoc 파일을 컨버팅하고 Build 폴더에 복사하기 위한 플러그인입니다.
    id 'org.asciidoctor.convert' version '2.4.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Spring Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Spring JPA
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.5.1'

    // H2
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'

    // Spring REST Docs
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // mockmvc 를 restdocs 에 사용할 수 있게 하는 라이브러리입니다.
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir

    // gradle build 시 test -> asciidoctor 순으로 수행됩니다.
    dependsOn test
}

bootJar {
    // gradle build 시 asciidoctor -> bootJar 순으로 수행됩니다.
    dependsOn asciidoctor

    // gradle build 시 ./build/asciidoc/html5/ 에 html 파일이 생깁니다.
    from ("${asciidoctor.outputDir}/html5") {

        // 이것을 jar 안에 /static/docs/ 폴더에 복사가 됩니다.
        into 'static/docs'
    }
}
